# test728 : kscript 데이터 내장기 (golang)

def conv(b) {
    m.malloc( "bconv", len(b) )
    for (i, r <- b) {
        m.set( "bconv", i, hex(r) )
    }
    temp = m.join("bconv", "")
    m.free("bconv")
    return "#"" +  temp + "#""
}

def gomode(data, pos, path) {
    num = len(data) // 80
    if (len(data) % 80 != 0) {
        num = num + 1
    }
    m.malloc("goenc", num + 2)
    m.set("goenc", 0, "func (tbox *Xe5) Data" + str(pos) + "() []byte {#n  data := #"#" // " + path)
    m.set("goenc", -1, "  return tbox.set(data, " + str(pos) + ")#n}")
    num = len(data) // 80
    for (i, r <- num) {
        m.set( "goenc", i + 1, "  data = data + " + conv( slice(data, 80 * i, 80 * i + 80) ) )
    }
    if (len(data) % 80 != 0) {
        num = len(data) // 80
        m.set( "goenc", num + 1, "  data = data + " + conv( slice(data, 80 * num, None) ) )
    }
    temp = m.join("goenc", "#n")
    m.free("goenc")
    return temp + "#n"
}

def main() {
    io.println("kscript datain golang")
    num = int( io.input("몇 개의 데이터를 내장합니까? : ") )
    m.malloc("datain", num + 3)
    m.set("datain", 0, "package main#n#nimport #"encoding/hex#"#n")
    m.set("datain", 1, "type Xe5 struct {#n  Data [" + str(num) + "][]byte#n}#n")
    m.set("datain", 2, "func (tbox *Xe5) set(data string, pos int) []byte {#n  tbox.Data[pos], _ = hex.DecodeString(data)#n  return tbox.Data[pos]#n}#n")
    for (i, r <- num) {
        path = io.input("파일 경로 입력 : ")
        if (slice(path, 0, 1) == "#"") {
            path = slice(path, 1, -1)
        }
        io.open(path, "r")
        data = io.read(path, -1)
        io.close(path)
        m.set( "datain", i + 3, gomode(data, i, path) )
    }
    temp = m.join("datain", "#n")
    m.free("datain")
    path = os.getpath("desktop") + "datain_result.go"
    io.open(path, "w")
    io.write(path, temp)
    io.close(path)
}

main()